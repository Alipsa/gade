import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

// Run `gradle runtimeZip` to create the distributions
plugins {
  id('java')
  id('jacoco')
  id('application')
  id('org.beryx.runtime').version('2.0.1')
  //id "com.github.spotbugs" version "5.0.9"
  // Allows us to do gradle dependencyUpdates -Drevision=release
  id("com.github.ben-manes.versions").version("0.53.0")
  id('signing')
  id('maven-publish')
  //id("org.openjfx.javafxplugin") version "0.1.0"
  id("se.alipsa.nexus-release-plugin").version'2.0.1'
  id("me.champeau.jmh").version("0.7.2") // JMH for performance benchmarks
}

group = 'se.alipsa'
version = '1.0.0-SNAPSHOT'
description = 'Gade, A Groovy Analytics Development Environment'
ext.nexusUrl = version.contains("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
ext.jarName = "${rootProject.name}-${version}.jar"
ext.releaseTag = version.endsWith('SNAPSHOT') ? "${version}-beta" : "${version}-GA"

ext.groovyVersion = "5.0.4"
ext.jacksonVersion = '2.21.0'
def jgitVersion = '7.5.0.202512021534-r' //'7.4.0.202509020913-r'
ext.log4jVersion = "2.25.3"
ext.tikaVersion = '3.2.3'
ext.openHtmlToPdfVersion = '1.1.37'

//def jdkVersion = "25+37" // Does not work with hadoop and hence not with matrix parquet
int javaLanguageVersion = 21
def jdkVersion = "${javaLanguageVersion}.0.10+10"

application {
  mainClass = 'se.alipsa.gade.Gade'
  applicationDefaultJvmArgs = [
      '--enable-native-access=javafx.graphics,javafx.media,javafx.web,ALL-UNNAMED',
      // Required for Gradle Tooling API to work with Java 21+
      '--add-opens=java.base/java.lang=ALL-UNNAMED',
      '--add-opens=java.base/java.util=ALL-UNNAMED',
      '--add-opens=java.base/java.io=ALL-UNNAMED',
      '--add-opens=java.base/java.net=ALL-UNNAMED'
  ]
}

/*javafx {
    version = '23.0.2'
    modules = [ 'javafx.controls', 'javafx.web', 'javafx.media', 'javafx.swing' ]
}*/

repositories {
  if (version.contains('SNAPSHOT')) {
    // Slows down the build (a lot), use only if local SNAPSHOTS are needed
    mavenLocal()
  }
  mavenCentral()
  maven {
    url = uri('https://repo.gradle.org/gradle/libs-releases')
  }
  maven {
    url = uri('https://jitpack.io')
  }
}

// Determine platform classifier for JavaFX dependencies
def getJavaFXPlatform() {
  def os = System.getProperty('os.name').toLowerCase()
  if (os.contains('mac')) {
    def arch = System.getProperty('os.arch').toLowerCase()
    return arch.contains('aarch64') || arch.contains('arm') ? 'mac-aarch64' : 'mac'
  } else if (os.contains('linux')) {
    return 'linux'
  } else if (os.contains('win')) {
    return 'win'
  }
  throw new GradleException("Unsupported OS: ${os}")
}

ext.javaFXPlatform = getJavaFXPlatform()
ext.javaFXVersion = '23.0.2'

dependencies {

  // JavaFX dependencies with platform classifiers for compilation
  compileOnly("org.openjfx:javafx-controls:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  compileOnly("org.openjfx:javafx-graphics:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  compileOnly("org.openjfx:javafx-base:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  compileOnly("org.openjfx:javafx-web:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  compileOnly("org.openjfx:javafx-media:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  compileOnly("org.openjfx:javafx-swing:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }

  implementation 'org.apache.commons:commons-text:1.15.0'
  implementation("org.apache.groovy:groovy-all:${groovyVersion}@pom") {
    transitive = true
  }
  implementation "org.apache.groovy:groovy-macro-library:${groovyVersion}"
  implementation "org.apache.groovy:groovy-ginq:$groovyVersion"
  //implementation 'org.apache.ivy:ivy:2.5.3' // needed for @Grab but included in groovy-console (part of groovy-all

  implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
  implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
  implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"

  implementation "org.apache.tika:tika-core:${tikaVersion}"
  implementation "org.apache.tika:tika-parsers-standard-package:${tikaVersion}"
  implementation "org.apache.tika:tika-parser-text-module:${tikaVersion}"
  implementation 'org.apache.commons:commons-lang3:3.20.0'
  implementation 'org.jsoup:jsoup:1.22.1'
  implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
  implementation 'org.fxmisc.richtext:richtextfx:0.11.7'
  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  implementation "com.fasterxml.jackson.core:jackson-annotations:2.21"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"

  implementation "commons-io:commons-io:2.21.0"
  implementation "org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}"
  implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:${jgitVersion}"

  implementation 'org.openjdk.nashorn:nashorn-core:15.7'
  implementation 'org.gradle:gradle-tooling-api:9.3.1'
  implementation 'org.jetbrains:annotations:26.0.2'
  implementation 'io.github.classgraph:classgraph:4.8.184'
  implementation 'com.github.spotbugs:spotbugs-annotations:4.9.8'
  implementation "se.alipsa.gi:gi-fx:0.3.0"
  implementation "org.commonmark:commonmark:0.27.1"
  implementation "org.commonmark:commonmark-ext-gfm-tables:0.27.1"
  implementation 'se.alipsa:fx-yearmonth-picker:1.1.0'
  implementation 'se.alipsa.groovy:data-utils:2.0.5'
  implementation('se.alipsa.gmd:gmd-core:3.0.2') {
    exclude group: 'se.alipsa.matrix', module: 'matrix-core'
    exclude group: 'se.alipsa.matrix', module: 'matrix-charts'
    exclude group: 'se.alipsa.matrix', module: 'matrix-xchart'
  }
  implementation("io.github.openhtmltopdf:openhtmltopdf-core:${openHtmlToPdfVersion}")
  implementation("io.github.openhtmltopdf:openhtmltopdf-pdfbox:${openHtmlToPdfVersion}")
  implementation("io.github.openhtmltopdf:openhtmltopdf-mathml-support:${openHtmlToPdfVersion}")
  implementation "io.github.openhtmltopdf:openhtmltopdf-svg-support:${openHtmlToPdfVersion}"

  implementation 'se.alipsa:simple-rest:1.1.1'
  implementation(platform("se.alipsa.matrix:matrix-bom:2.4.1-SNAPSHOT"))
  implementation("se.alipsa.matrix:matrix-core")
  implementation("se.alipsa.matrix:matrix-stats")
  implementation("se.alipsa.matrix:matrix-charts")
  implementation("se.alipsa.matrix:matrix-xchart")
  implementation("se.alipsa.matrix:matrix-bigquery")
  implementation("se.alipsa.matrix:matrix-sql")
  // Used by se.alipsa.gade.utils.SemanticVersion
  implementation 'org.apache.maven:maven-artifact:3.9.11'
  implementation 'org.apache.maven:maven-model:3.9.11'
  implementation 'se.alipsa:maven-3.9.11-utils:1.2.0'

  implementation 'se.alipsa.groovy:dependency-resolver:1.0.1'
  //implementation 'com.github.hervegirod:fxsvgimage:1.4'
  implementation 'com.github.jsqlparser:jsqlparser:5.3'
  implementation('com.manticore-projects.jsqlformatter:jsqlformatter:5.3.7') {
    exclude group: 'com.github.jsqlparser', module: 'jsqlparser'
  }

  testImplementation platform('org.junit:junit-bom:6.0.2')
  testImplementation 'com.h2database:h2:2.4.240'
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'net.jodah:concurrentunit:0.4.6'
  testImplementation "org.eclipse.jgit:org.eclipse.jgit.http.server:${jgitVersion}"
  testImplementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'
  testImplementation 'org.eclipse.jetty:jetty-server:12.1.6'
  testImplementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.1.6'
  testImplementation 'org.testfx:testfx-core:4.0.18'
  testImplementation 'org.testfx:testfx-junit5:4.0.18'
  testImplementation 'org.testfx:openjfx-monocle:21.0.2'
  // JavaFX dependencies for testing only (runtime distributions use bundled JavaFX from platform JDKs)
  testImplementation("org.openjfx:javafx-controls:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation("org.openjfx:javafx-graphics:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation("org.openjfx:javafx-base:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation("org.openjfx:javafx-web:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation("org.openjfx:javafx-media:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation("org.openjfx:javafx-swing:${javaFXVersion}:${javaFXPlatform}") {
    transitive = false
  }
  testImplementation 'se.alipsa.gi:gi-console:0.3.0'
  testImplementation 'org.mockito:mockito-core:5.21.0'
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(javaLanguageVersion))
  }
  //withSourcesJar()
  //withJavadocJar()
}

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

/*
tasks.withType(GroovyCompile) { task ->
  task.options.deprecation = true
  task.options.compilerArgs += ['--upgrade-module-path', fxUpgradePath.get()]
}*/

static def getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyy-MMM-dd_HH.mm.ss')
  return formattedDate
}

// due to late binding, the jdkVersion needs to be passed as a parameter
def deleteIfDiff(String jdkVersion, String path) {
  boolean platformDiff = true
  if (jdkVersion.contains('+')) {
    String matchString = jdkVersion.substring(0, jdkVersion.indexOf('+'))
    logger.info("matching $matchString on platform jdks")
    file(path).listFiles().each {
      if (it.getName().contains(matchString)) {
        platformDiff = false
      }
    }
  }
  if (platformDiff) {
    logger.info("$jdkVersion not found in $path, deleting it")
    delete path
  }
}

clean {
  // include the platform jdks as otherwise, updating the version will not "take"
  deleteIfDiff(jdkVersion, "$projectDir/platform/linux")
  deleteIfDiff(jdkVersion, "$projectDir/platform/win")
  deleteIfDiff(jdkVersion, "$projectDir/platform/mac")
}

processResources.doLast {

  def extractVersionNumber = { artifactName, fileName ->
    fileName.substring("${artifactName}-".length(), fileName.indexOf(".jar"))
  }

  configurations.getByName('runtimeClasspath').files.each { file ->
    def fileName = file.name
    if (fileName.contains('matrix-core')) {
      ext.matrixCoreVersion = extractVersionNumber('matrix-core', fileName)
    } else if (fileName.contains('matrix-charts')) {
      ext.matrixChartsVersion = extractVersionNumber('matrix-charts', fileName)
    } else if (fileName.contains('matrix-stats')) {
      ext.matrixStatsVersion = extractVersionNumber('matrix-stats', fileName)
    } else if (fileName.contains('matrix-xchart')) {
      ext.matrixXchartVersion = extractVersionNumber('matrix-xchart', fileName)
    } else if (fileName.contains('matrix-bigquery')) {
      ext.matrixBigQueryVersion = extractVersionNumber('matrix-bigquery', fileName)
    } else if (fileName.contains('matrix-sql')) {
      ext.matrixSqlVersion = extractVersionNumber('matrix-sql', fileName)
    }
    /*else if (fileName.contains('matrix-datasets')) {
      ext.matrixDatasetsVersion = extractVersionNumber('matrix-datasets', fileName)
    } else if (fileName.contains('matrix-spreadsheet')) {
      ext.matrixSpreadsheetVersion = extractVersionNumber('matrix-spreadsheet', fileName)
    } else if (fileName.contains('matrix-json')) {
      ext.matrixJsonVersion = extractVersionNumber('matrix-json', fileName)
    } else if (fileName.contains('matrix-csv')) {
      ext.matrixCsvVersion = extractVersionNumber('matrix-csv', fileName)
    } else if (fileName.contains('matrix-parquet')) {
      ext.matrixParquetVersion = extractVersionNumber('matrix-parquet', fileName)
    } else if (fileName.contains('matrix-tablesaw')) {
      ext.matrixTablesawVersion = extractVersionNumber('matrix-tablesaw', fileName)
    } else if (fileName.contains('matrix-gsheets')) {
      ext.matrixGsheetsVersion = extractVersionNumber('matrix-gsheets', fileName)
    } else if (fileName.contains('matrix-avro')) {
      ext.matrixAvroVersion = extractVersionNumber('matrix-avro', fileName)
    }*/ else if (fileName.contains('gmd-core')) {
      ext.gmdCoreVersion = extractVersionNumber('gmd-core', fileName)
    }
  }

  ant.propertyfile(file: "${projectDir}/version.properties") {
    ant.entry(key: "version", value: "${version}")
    ant.entry(key: "jarName", value: "${jarName}")
    ant.entry(key: "releaseTag", value: "${releaseTag}")
    ant.entry(key: "buildDate", value: "${getDate()}")
    ant.entry(key: "matrixCoreVersion", value: "${matrixCoreVersion}")
    ant.entry(key: "matrixChartsVersion", value: "${matrixChartsVersion}")
    ant.entry(key: "matrixStatsVersion", value: "${matrixStatsVersion}")
    //ant.entry(key: "matrixDatasetsVersion", value: "${matrixDatasetsVersion}")
    //ant.entry(key: "matrixSpreadsheetVersion", value: "${matrixSpreadsheetVersion}")
    //ant.entry(key: "matrixJsonVersion", value: "${matrixJsonVersion}")
    //ant.entry(key: "matrixCsvVersion", value: "${matrixCsvVersion}")
    ant.entry(key: "matrixSqlVersion", value: "${matrixSqlVersion}")
    //ant.entry(key: "matrixParquetVersion", value: "${matrixParquetVersion}")
    ant.entry(key: "matrixBigQueryVersion", value: "${matrixBigQueryVersion}")
    ant.entry(key: "matrixXchartVersion", value: "${matrixXchartVersion}")
    //ant.entry(key: "matrixTablesawVersion", value: "${matrixTablesawVersion}")
    //ant.entry(key: "matrixAvroVersion", value: "${matrixAvroVersion}")
    ant.entry(key: "gmdCoreVersion", value: "${gmdCoreVersion}")
    //ant.entry(key: "matrixGsheetsVersion", value: "${matrixGsheetsVersion}")
  }
  ant.copy(
      file: "${projectDir}/version.properties",
      //todir: "${buildDir}/resources/main"
      todir: layout.buildDirectory.dir("resources/main").get().asFile
  )
}

test {
  // Note: JavaFX module args (--add-exports, --add-opens, --enable-native-access) are not needed
  // because JavaFX is on the classpath (via testImplementation), not the module path

  // Headless mode is enabled by default using Monocle
  // Usage: ./gradlew test (headless, no windows)
  //        ./gradlew test -Dheadless=false (with windows)
  if (System.getProperty('headless', 'true') != 'false') {
    systemProperty 'glass.platform', 'Monocle'
    systemProperty 'monocle.platform', 'Headless'
    // Note: prism.order=sw can cause crashes on macOS/Windows, only set on Linux
    if (!System.getProperty('os.name').toLowerCase().contains('mac') &&
        !System.getProperty('os.name').toLowerCase().contains('win')) {
      systemProperty 'prism.order', 'sw'
    }
    systemProperty 'testfx.headless', 'true'
    systemProperty 'testfx.robot', 'glass'
    systemProperty 'java.awt.headless', 'true'
    systemProperty 'prism.text', 't2k'
    // Set screen size large enough for Gade's window
    systemProperty 'headless.geometry', '1920x1080-32'
    // Prevent Monocle crash on JVM shutdown by keeping platform alive
    systemProperty 'glass.noImplicitExit', 'true'
    systemProperty 'glass.gtk.disableGtkTk', 'true'
    systemProperty 'com.sun.javafx.isEmbedded', 'true'
  }

  // Skip TestFX tests with -DskipTestFx=true or automatically on macOS (known SIGTRAP crashes)
  // Usage: ./gradlew test -DskipTestFx=true
  // Override macOS auto-skip: ./gradlew test -DskipTestFx=false
  boolean shouldSkipTestFx = System.getProperty('skipTestFx') != null
      ? System.getProperty('skipTestFx') == 'true'
      : System.getProperty('os.name').toLowerCase().contains('mac')

  if (shouldSkipTestFx) {
    if (System.getProperty('os.name').toLowerCase().contains('mac')) {
      logger.lifecycle('Skipping TestFX tests on macOS due to known SIGTRAP crashes. Run with -DskipTestFx=false to enable.')
    }
    exclude '**/GadeSmokeTest.class'
    exclude '**/*FxTest.class'
    exclude '**/*TestFx.class'
  }

  // On macOS with TestFX enabled, warn about known crash but don't set ignoreFailures
  // (ignoreFailures doesn't prevent JVM crash exit codes, and we want real test failures to be caught)
  if (System.getProperty('os.name').toLowerCase().contains('mac') && !shouldSkipTestFx) {
    logger.warn('TestFX tests enabled on macOS - JVM may crash with SIGTRAP after tests complete.')
    logger.warn('This is a known Monocle issue. Tests pass successfully before the crash.')
    logger.warn('If you see BUILD FAILED with SIGTRAP, check test results - they likely all passed.')
  }

  useJUnitPlatform()
  testLogging {
    // set options for log level LIFECYCLE
    events TestLogEvent.FAILED,
        //TestLogEvent.PASSED,
        TestLogEvent.SKIPPED,
        TestLogEvent.STANDARD_OUT
    exceptionFormat = TestExceptionFormat.FULL
    showExceptions = true
    showCauses = true
    showStackTraces = true

    // set options for log level DEBUG and INFO
    debug {
      events TestLogEvent.STARTED,
          TestLogEvent.FAILED,
          TestLogEvent.PASSED,
          TestLogEvent.SKIPPED,
          TestLogEvent.STANDARD_ERROR,
          TestLogEvent.STANDARD_OUT
      exceptionFormat = TestExceptionFormat.FULL
    }
    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    html.required = true
    csv.required = false
  }
}

test.finalizedBy jacocoTestReport

runtime {
  //options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  // If there are issues, consider including more modules, see https://docs.oracle.com/en/java/javase/17/docs/api/index.html
  modules.set(['javafx.controls', 'javafx.web', 'javafx.media', 'javafx.swing', 'java.scripting',
               'java.management', 'java.se', 'java.sql', 'java.naming', 'jdk.crypto.ec', 'java.prefs',
               'java.xml', 'java.compiler', 'jdk.compiler', 'java.desktop', 'jdk.dynalink',
               'jdk.security.auth', 'java.security.jgss', 'java.instrument', 'jdk.zipfs',
               'jdk.jartool', 'jdk.javadoc'
  ])

  launcher {
    noConsole = true
    jvmArgs = [
        '--enable-native-access=javafx.graphics,javafx.media,javafx.web,ALL-UNNAMED',
        // Required for Gradle Tooling API to work with Java 21+
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED',
        '--add-opens=java.base/java.io=ALL-UNNAMED',
        '--add-opens=java.base/java.net=ALL-UNNAMED'
    ]
  }
  // check for latest FULL versions here: https://bell-sw.com/pages/downloads/#/java-21-lts
  def baseUrl = "https://download.bell-sw.com/java"

  targetPlatform("linux") {
    jdkHome = jdkDownload("${baseUrl}/${jdkVersion}/bellsoft-jdk${jdkVersion}-linux-amd64-full.tar.gz") {
      downloadDir = "$projectDir/platform/linux"
    }
  }
  targetPlatform("win") {
    jdkHome = jdkDownload("${baseUrl}/${jdkVersion}/bellsoft-jdk${jdkVersion}-windows-amd64-full.zip") {
      downloadDir = "$projectDir/platform/win"
    }
  }
  targetPlatform("mac") {
    jdkHome = jdkDownload("${baseUrl}/${jdkVersion}/bellsoft-jdk${jdkVersion}-macos-aarch64-full.tar.gz") {
      downloadDir = "$projectDir/platform/mac"
    }
  }
}

tasks.runtime.doLast {
  // Reorganize jars into subdirectories for clean classpath isolation
  // lib/groovy/   - Groovy + Ivy jars (subprocess + Gade app)
  // lib/app/      - Gade application dependencies (Gade app only)
  // lib/runtimes/ - gade-runner.jar (subprocess only)
  ['linux', 'mac', 'win'].each { platform ->
    def libDir = project.layout.buildDirectory.dir("image/gade-${platform}/lib").get().asFile

    // Create subdirectories
    def groovyDir = new File(libDir, 'groovy')
    def appDir = new File(libDir, 'app')
    def runtimesDir = new File(libDir, 'runtimes')
    groovyDir.mkdirs()
    appDir.mkdirs()
    runtimesDir.mkdirs()

    // Move groovy and ivy jars to lib/groovy/
    libDir.listFiles({ File f ->
      f.isFile() && f.name.endsWith('.jar') &&
      (f.name.startsWith('groovy-') || f.name == 'groovy.jar' || f.name.startsWith('ivy-'))
    } as FileFilter)?.each { it.renameTo(new File(groovyDir, it.name)) }

    // Move groovy-all .pom file to lib/groovy/ (part of the Groovy distribution)
    libDir.listFiles({ File f ->
      f.isFile() && f.name.endsWith('.pom') &&
      (f.name.startsWith('groovy-'))
    } as FileFilter)?.each { it.renameTo(new File(groovyDir, it.name)) }

    // Copy gade-runner.jar to lib/runtimes/
    copy {
      from runnerJar.archiveFile
      into runtimesDir
    }

    // Move remaining jars to lib/app/
    libDir.listFiles({ File f ->
      f.isFile() && f.name.endsWith('.jar')
    } as FileFilter)?.each { it.renameTo(new File(appDir, it.name)) }
  }

  copy {
    from("$projectDir/src/main/resources/script/")
    into(project.layout.buildDirectory.dir("image/gade-linux/"))
    include 'createLauncher.sh'
    include '*.png'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-linux/"))
    include '*.sh'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-linux/conf"))
    include 'jaas.conf'
  }

  copy {
    from("$projectDir/src/main/resources/script/")
    into(project.layout.buildDirectory.dir("image/gade-mac/"))
    include '*.png'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-mac/"))
    include '*.sh'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-mac/conf"))
    include 'jaas.conf'
  }

  copy {
    from("$projectDir/src/main/resources/script/")
    into(project.layout.buildDirectory.dir("image/gade-win/"))
    include '*.ico'
    include '*.ps1'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-win/"))
    exclude 'jaas.conf'
  }
  copy {
    from("$projectDir/src/bin/cponly")
    into(project.layout.buildDirectory.dir("image/gade-win/conf"))
    include 'jaas.conf'
  }

  copy {
    from("$projectDir")
    into(project.layout.buildDirectory.dir("image/gade-linux/"))
    include 'version.properties'
  }
  copy {
    from("$projectDir")
    into(project.layout.buildDirectory.dir("image/gade-mac/"))
    include 'version.properties'
  }
  copy {
    from("$projectDir")
    into(project.layout.buildDirectory.dir("image/gade-win/"))
    include 'version.properties'
  }
}

tasks.distTar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
tasks.distZip.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
tasks.installDist.duplicatesStrategy = DuplicatesStrategy.EXCLUDE

tasks.runtimeZip.doLast {

  //ant.move(todir:"$project.buildDir") {
  ant.move(todir: project.layout.buildDirectory.get().asFile) {
    //ant.fileset(dir: "$project.buildDir") {
    ant.fileset(dir: project.layout.buildDirectory.get().asFile) {
      include(name: "image-*.zip")
    }
    ant.mapper(type: "glob", from: "image-*.zip", to: "${project.name}-*-${version}.zip")
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  resolutionStrategy {
    componentSelection {
      all {
        if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
          reject('Release candidate')
        }
      }
    }
  }
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  archiveClassifier.set('javadoc')
}


task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

// Separate JAR for the subprocess runner â€” contains only the classes needed
// by GadeRunnerMain. No Jackson, no Log4j, no Gade application classes.
task runnerJar(type: Jar, dependsOn: classes) {
  archiveBaseName = 'gade-runner'
  from sourceSets.main.output
  include 'se/alipsa/gade/runner/**'
  include 'se/alipsa/gade/runtime/ProtocolVersion**'
  include 'se/alipsa/gade/runtime/ProtocolXml**'
}

// Ensure runnerJar is built before run and runtime tasks
tasks.named('run').configure { dependsOn runnerJar }
tasks.named('runtime').configure { dependsOn runnerJar }

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Gade'
        description = "${project.description}"
        url = "https://github.com/Alipsa/gade"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/gade/main/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/gade/tree/main'
          connection = 'scm:git:https://github.com/Alipsa/gade.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/gade.git'
        }
        /*
        build {
            plugin {
                groupId='org.openjfx'
                artifactId='javafx-maven-plugin'
                version='0.0.8'
                configuration {
                    mainClass='se.alipsa.gade.Gade'
                }
            }
        }*/
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        url = nexusUrl
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

if (project.ext.properties.sonatypeUsername) {

  apply plugin: 'se.alipsa.nexus-release-plugin'
  nexusReleasePlugin.nexusUrl = nexusUrl
  nexusReleasePlugin.userName = sonatypeUsername
  nexusReleasePlugin.password = sonatypePassword
}
